buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'org.github.ngbinh.scalastyle:gradle-scalastyle-plugin_2.11:0.8.2'
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
        classpath 'com.novoda:bintray-release:0.3.2'
    }
}

apply plugin: 'idea'
apply plugin: 'scala'
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'scalaStyle'
apply plugin: 'com.novoda.bintray-release'

version = "0.5"
group = "com.easyforger"
archivesBaseName = "easyforger-samples"

scalaStyle {
    configLocation = "project/scalastyle_config.xml"
    includeTestSourceDirectory = true
    source = "src/main/scala"
    testSource = "src/test/scala"
}

compileScala.dependsOn scalaStyle

tasks.withType(ScalaCompile) {
    scalaCompileOptions.useAnt = false
    scalaCompileOptions.additionalParameters = ["-language:implicitConversions"]
}

repositories {
    mavenLocal()
    jcenter()
}

dependencies {
    compile 'com.easyforger:easyforger-core:' + project.version + ':deobf@jar'
}

minecraft {
    version = "1.10.2-12.18.2.2099"
    runDir = "run"
    mappings = "snapshot_20161103"
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

idea {
    module {
        inheritOutputDirs = true
    }
}

import com.novoda.gradle.release.*

def deobfJarPublish(String publicationName, Project project) {
    project.task(publicationName + 'DeobfJar', type: Jar) {
        classifier = 'deobf'
        from sourceSets.main.output
    }
}

publish {
    bintrayUser = project.hasProperty('myBintrayUser') ? myBintrayUser : 'bintray-user'
    bintrayKey = project.hasProperty('myBintrayKey') ? myBintrayKey : 'bintray-key'

    userOrg = 'easyforger'
    groupId = project.group
    artifactId = archivesBaseName

    publishVersion = project.version
    description = 'Samples using the EasyForger library.'

    website = 'https://easyforger.com'
    issueTracker = 'https://github.com/easyforger/easyforger-samples/issues'
    repository = 'git@github.com:easyforger/easyforger-samples.git'

    dryRun = true

    publishing {
        publications {
            deobfPublication(MavenPublication) {
                groupId project.publish.groupId
                artifactId project.publish.artifactId
                publishVersion project.publish.publishVersion

                Artifacts artifacts = new JavaArtifacts()
                (artifacts.all(it.name, project) + deobfJarPublish(it.name, project)).each {
                    delegate.artifact it
                }
                from artifacts.from(project)
            }
        }
    }

    publications = ['deobfPublication']
}
